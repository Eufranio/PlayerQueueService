plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group = pluginGroup
version = pluginVersion

subprojects {
    apply plugin: 'org.spongepowered.plugin'
    version = this.version
    dependencies {
        compileOnly 'org.spongepowered:spongeapi:7.2.0-SNAPSHOT'
    }
}

project(':api') {
    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
    artifacts.archives sourcesJar
}

dependencies {
    compileOnly 'org.spongepowered:spongeapi:7.2.0-SNAPSHOT'
    annotationProcessor 'org.spongepowered:spongeapi:7.2.0-SNAPSHOT'

    shadow project(':api')
    shadow 'com.github.Eufranio:StorageUtils:2.2'
}

repositories {
    maven { url 'https://jitpack.io' }
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier = null
    from sourceSets.main.output
}

task copyJars(type: Copy) {
    from([subprojects.jar, project(':api').sourcesJar, shadowJar])
    into project.file("builds/${version}")
}

build.dependsOn copyJars

sponge.plugin.id = pluginId
